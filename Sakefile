use 'sake-outdated'
use 'sake-publish'
use 'sake-version'

task 'clean', 'clean project', ->
  exec 'rm -rf lib && rm -rf bin/handroll'

task 'bootstrap', 'Build bootstrapped version of handroll', ->
  coffee      = require '@zeekay/rollup-plugin-coffee'
  nodeResolve = require '@zeekay/rollup-plugin-node-resolve'
  json        = require 'rollup-plugin-json'
  rollup      = require 'rollup'

  pkg      = require './package.json'
  external = (Object.keys pkg.dependencies).concat Object.keys pkg.devDependencies

  plugins  = [
    coffee()
    json()
    nodeResolve()
  ]

  # CommonJS bootstrap lib
  bundle = yield rollup.rollup
    input:     'src/index.coffee'
    acorn:     allowReserved: true
    external:  external
    plugins:   plugins

  yield bundle.write
    file:      'lib/bootstrap.js'
    format:    'cjs'
    sourcemap: true

task 'build', 'build project', ['bootstrap'], ->
  handroll = require './lib/bootstrap.js'
  path     = require 'path'

  b = new handroll.Bundle external: true

  yield b.write
    entry:    'src/index.coffee'
    formats:  ['cjs', 'es']
    inject:
      exclude: 'node_modules/**',
      'Object.assign': path.resolve 'node_modules/es-object-assign/lib/es-object-assign.mjs'

  yield b.write
    entry:      'src/cli.coffee'
    format:     'cli'
    executable: true

task 'watch', 'watch project and build on changes', ->
  build = (filename) ->
    console.log filename, 'modified, rebuilding'
    invoke 'build' if not running 'build'
  watch 'src/',          build
  watch 'node_modules/', build, watchSymlink: true

task 'test', 'test handroll', ['build']
